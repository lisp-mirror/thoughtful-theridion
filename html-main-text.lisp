(in-package :thoughtful-theridion)

;;; Stretch goal: implement https://github.com/mozilla/readability/ algorithm

(defun html-score-content (protocol element cache)
  "Evaluate content density in ELEMENT according to PROTOCOL.
Use CACHE as a hash-table of precomputed results. This function allows CACHE
reuse with the same PROTOCOL.

Return values: total score, list of scores by level (own, children, etc.),
text that the parent node may consider as its own for scoring, the data to use
as readable content in case this element is picked as a part of main content.
"
  (let* ((cached (gethash element cache))
         (data (or cached
                   (multiple-value-list
                     (html-score-content-dispatch
                       protocol (html5-parser:node-type element)
                       (and (html5-parser:node-name element)
                            (intern (string-upcase (html5-parser:node-name element))
                                    :keyword))
                       element cache)))))
    (unless cached (setf (gethash element cache) data))
    (apply 'values data)))

(defgeneric html-score-content-dispatch (protocol type tag element cache))

(defmacro with-html-score-content-protocol ((protocol-type) (protocol element &optional (cache (gensym)))
                                            &body body)
  `(symbol-macrolet ((html-score-content-protocol-type ,protocol-type)
                     (html-score-content-protocol ,protocol)
                     (html-score-content-element ,element)
                     (html-score-content-cache ,cache))
                    ,@body))

(defmacro def-html-score-content ((type tags) &body body &environment env)
  (let* ((type-var (gensym))
         (tag-var (if (or (null (first tags)) (keywordp (first tags)))
                    (gensym) (first tags)))
         (tag-options (remove-if (lambda (x) (and x (not (keywordp x)))) tags)))
    `(progn
       ,@(loop for tag-option in tag-options collect
               `(defmethod html-score-content-dispatch
                  ((,(macroexpand-1 'html-score-content-protocol env)
                     ,(macroexpand-1 'html-score-content-protocol-type env))
                   (,type-var (eql ,type))
                   (,tag-var (eql ,tag-option))
                   (,(macroexpand-1 'html-score-content-element env) t)
                   (,(macroexpand-1 'html-score-content-cache env) hash-table))
                  ,@body)))))

(defclass html-text-score-protocol ()
  ())

(defgeneric html-score-text (protocol text))

(defvar *unicode-property-cache* (make-hash-table :test 'equal))

(defun unicode-property-counts (text &rest properties)
  (apply 'values
         (loop for p in properties
               collect (count-if
                         (lambda (x)
                           (multiple-value-bind
                             (value presentp)
                             (gethash (list x p) *unicode-property-cache*)
                             (if presentp value
                               (setf
                                 (gethash (list x p) *unicode-property-cache*)
                                 (cl-unicode:has-property x p)))))
                         text))))

(defmethod html-score-text ((protocol html-text-score-protocol) (text null)) 0)
(defmethod html-score-text ((protocol html-text-score-protocol) (text string))
  (multiple-value-bind
    (letters sentences punctuation)
    (unicode-property-counts
      text
      "Alphabetic" "Sentence_Terminal" "Punctuation")
    (let* ((commas (- punctuation sentences))
           (length-points (min 3 (truncate letters 90)))
           (sentence-points (min 20 (truncate sentences 5))))
      (if (< letters 25) 0 (+ commas length-points sentence-points 1)))))

(defmethod html-content-score-summarize ((protocol html-text-score-protocol)
                                         (scores list))
  (case (length scores)
    (0 0)
    (1 (first scores))
    (t (+ (first scores) (/ (second scores) 2)
          (loop for x in (cddr scores)
                for k upfrom 1
                sum (/ x 3 k))))))

(defun list-binary-+ (&optional a b)
  (cond ((null a) b)
        ((null b) a)
        (t (cons (+ (first a) (first b))
                 (list-binary-+ (rest a) (rest b))))))

(defun list-+ (&rest lists)
  (reduce #'list-binary-+ lists :initial-value '()))

(defun collect-child-scoring-data (protocol element cache)
  (let* ((top-level-scores (list))
         (texts (list))
         (content-children (list)))
    (loop for c := (html5-parser:node-first-child element)
          then (html5-parser:node-next-sibling c)
          while c do
          (multiple-value-bind
            (score level-scores text content)
            (html-score-content protocol c cache)
            (declare (ignorable score))
            (push level-scores top-level-scores)
            (when text (push text texts))
            (when content (push content content-children))))
    (let* ((own-text (apply 'concatenate 'string (reverse texts)))
           (sub-scores (apply 'list-+ top-level-scores))
           (content (cons element (reverse content-children))))
      (values sub-scores own-text content))))

(defmethod html-score-content-dispatch ((protocol html-text-score-protocol)
                                        type tag element cache)
  (multiple-value-bind
    (sub-scores own-text content)
    (collect-child-scoring-data protocol element cache)
    (let* ((own-direct-score (html-score-text protocol own-text))
           (all-level-scores (cons own-direct-score sub-scores))
           (score (html-content-score-summarize protocol all-level-scores)))
      (values score all-level-scores nil content))))

(with-html-score-content-protocol
  (html-text-score-protocol) (protocol element cache)
  (def-html-score-content (:comment (nil)) (values 0 () "" nil))
  (def-html-score-content (:document-type (nil)) (values 0 () "" nil))
  (def-html-score-content (:text (nil))
    (let* ((text 
             (cl-ppcre:regex-replace-all
               (if *whitespace-list*
                 (format nil "[狺莴麒轸弩疳沐扉篝ㄨ繇斓疳蝮弪侯镤瀛鲠祯屐屙孱舂┅筱矧ㄨ繇飙筱矧瀛翦痱雉镢镬翦舂┅鲠祯弩筱矧扉篝翦屐屙孱舂┅ㄤ彐梏盱筱矧瀛泔铘孱ê滹沲礤铘铋飑ㄣ犰飙铄舡礤翳镤┅ㄤ彐梏盱筱矧瀛泔铘孱ê滹沲礤铘骝徵礤铘铋飑ㄣ犰飙铄舡礤翳镤┅ㄤ彐梏盱筱矧瀛泔铘孱ê屐屙孱ê怛┅鲠祯弩扉篝屐屙孱舂ㄤ彐梏盱筱矧瀛泔铘孱ê屐屙孱ê篝戾后泸轲鸿遽洎鲠祯弩扉篝铋铋飑ㄤ彐梏盱筱矧瀛泔铘孱ê屐屙孱ê岍眭祠轲戾鲠祯瀛忾钿篚猸筱矧弩秣瞽翦泔铘孱舂ㄣ镬戾泗汨殪洵筱矧轭绛溽翎痱雉镢镬屐屙孱汜汨濠ㄤ邈灬蝈ㄩ珙矧徕戾篚猸筱矧弩┅鲠祯弩扉篝爱暴秣瞽翦泔铘孱舂┅ㄤ彐梏盱筱矧瀛泔铘孱ê屐屙孱ê徕怛衡衡滹恒轸恒镤轰狒轰狒犰轶轰骖哄洪弘怃红徕屐喉狎喉狒喉弭弪侯矬泸轲猴豸瘐吼蝻珧弩厚候踱后犴后磲祆后疳后趄镱后踱后躔呼轫忽狎瑚怛鸿鸿鸿鸿鸿鸿后邈糸镱吼呼吼蝈┅眭祠轲戾鲠祯瀛忾钿篚猸筱矧弩秣瞽翦泔铘孱舂ㄣ镬戾泗汨殪洵筱矧轭绛溽翎痱雉镢镬屐屙孱汜汨濠ㄤ邈灬蝈ㄩ珙矧徕戾篚猸筱矧弩┅鲠祯弩ㄨ繇飙筱矧瀛翦痱雉镢镬秣瞽翦舂扉篝秣瞽翦泔铘孱舂┅ㄤ彐礤翳镤怩殪洵泔铘孱舡滹è痱雉镢镬梏盱翦舡筱矧瀛痱雉镢镬ㄣ镱翦铘梏盱淡疳蝮弪汉滹沲礤铘疳蝈铘滹沲礤铘ㄤ邈灬蝈ㄩ珙矧徕戾泔铘孱疳蝈铘滹沲礤铘┅ㄨ繇斓疳蝮弪喉犭瀛滹沲礤铘┅ㄤ彐礤翳镤怩殪洵泔铘孱舡滹è痱雉镢镬梏盱翦舡筱矧瀛痱雉镢镬ㄣ镱翦铘梏盱淡疳蝮弪汉滹沲礤铘骝徵礤铘疳蝈铘滹沲礤铘ㄤ邈灬蝈ㄩ珙矧徕戾泔铘孱疳蝈铘滹沲礤铘┅ㄨ繇斓疳蝮弪喉犭瀛骝徵礤铘滹沲礤铘┅ㄤ彐礤翳镤怩殪洵泔铘孱舡滹è痱雉镢镬梏盱翦舡筱矧瀛痱雉镢镬泔铘孱疳蝈铘铛祆滹沲礤铘戾舄è疳蝈铘ㄨ繇斓疳蝮弪喉犭瀛滹沲礤铘┅ㄣ栝熹ㄢ蹰熹泔铘孱舡滹痱雉镢镬泔铘孱疳蝈铘疳蝈铘┅ㄩㄨ繇斓疳蝮弪侯镤瀛骈蝮舡汨殪疳蝈铘疳蝈铘汨殪洎┅ㄤ彐礤翳镤怩殪洵泔铘孱舡滹è痱雉镢镬梏盱翦舡筱矧瀛痱雉镢镬ㄣ镱翦铘扉篝疳蝈铘梏盱淡疳蝮弪汉铒溴滹沲礤铘戾舄è屐屙孱ㄢ蹰熹泔铘孱舡滹痱雉镢镬ㄦ轵篝泔铘孱舂疳蝈铘滹沲礤铘┅祜镳骘轭蝈篝泔铘孱舂滹ㄢ蹰熹泔铘孱舡滹痱雉镢镬屐屙孱滹沲礤铘┅屐屙孱舂ㄤ彐礤翳镤怩殪洵泔铘孱舡滹è痱雉镢镬梏盱翦舡筱矧瀛痱雉镢镬ㄣ镱翦铘梏盱淡疳蝮弪汉铒溴疳蝈铘梏盱淡疳蝮弪汉铒溴滹沲礤铘铋飑ㄤ彐礤翳镤怩殪洵泔铘孱舡滹è痱雉镢镬梏盱翦舡筱矧瀛痱雉镢镬ㄣ镱翦铘梏盱淡疳蝮弪汉屐屙孱舂疳蝈铘梏盱淡疳蝮弪汉铒溴滹沲礤铘戾舄è屐屙孱ㄨ繇斓疳蝮弪喉犭瀛屐屙孱滹沲礤铘ㄨ繇斓疳蝮弪侯镤瀛钺礤泔铘孱舂ㄨ繇斓疳蝮弪侯镤瀛钺礤箴徙泔铘孱舂┅ㄨ繇斓疳蝮弪哄戾礤铘磲瓠狒趄殁豸弩灬礅溽钺礤钺礤箴徙鲠祯濠箦翩ㄨ繇斓疳蝮弪哄戾礤铘狒趄殁豸屐屙孱钺礤钺礤箴徙濠鲠祯濠泔铘孱舂ㄨ繇斓疳蝮弪侯镤瀛狃疱钿汨殪疳蝈铘屐屙孱舂屐屙孱舂ㄤ彐礤翳镤怩殪洵泔铘孱舡滹è痱雉镢镬梏盱翦舡筱矧瀛痱雉镢镬ㄣ镱翦铘梏盱淡疳蝮弪汉翦舡铒溴疳蝈铘梏盱淡疳蝮弪汉铒溴滹沲礤铘戾舄è屐屙孱ㄨ繇斓疳蝮弪喉犭瀛翦舡铒溴滹沲礤铘ㄨ繇斓疳蝮弪侯镤瀛鲠祯泔铘孱舂┅ㄨ繇斓疳蝮弪侯镤瀛狃疱钿汨殪疳蝈铘屐屙孱舂屐屙孱舂ㄤ彐躅梏盱屮趄徙舡磲轭泔铘孱ㄤ狒筱矧瀛痱雉镢镬翦糸纟痱雉镢镬戾舄è汜汨磲脲栳箬翎忪呼弩у聃犰┅ㄡ蜱磲铋飑磲筱矧癌翎蜱弭铋飑ㄨ繇飙筱矧瀛泔铘孱筱矧瀛痱雉镢镬溽翎汜汨濠磲痂狍灬礅溽雯ㄩㄦ轵篝雯磲筱矧濠箦翩狎珥狲磲筱矧ㄦ轵篝雯┅汜汨濠箦翩翎蜱弭狎珥狲箦翩翎蜱弭ㄢ蹰熹泔铘孱舡滹筱矧瀛痱雉镢镬ㄦ秕螋眭祠轲戾鲠祯瀛扉篝ㄨ繇飙筱矧瀛泔铘孱筱矧瀛痱雉镢镬翎蜱弭汜汨濠┅铋铋飑ㄩ翦糸纟痱雉镢镬ㄨ繇飙屐屙孱舡麸翦翦糸纟痱雉镢镬翎蜱弭翎蜱弭┅ㄤ彐沆狍梏盱沆狍箢犴瀛筱矧瀛痱雉镢镬ㄨ繇飙翦舡筱矧瀛痱雉镢镬īㄤ彐疳蜥礤翦蝈盹鲠忪瀛沆狍箢犴瀛蝈珏皙徜衢茶繇禳忉铑弪怛遽溷蝓礅簏泔礅泔眄孱酎泔眄躅轸泔鲥颦黩狃溟篑躞屮趄狳骘雉弪玟痱桢徜弪戾珏钿簏礤铛蝈灬翦潼蝈磲螂蝈痨殄簏蝮簏箬秕翕秫箝溴忉螯箅筱蜥疱螯箫汩犰箴镱箫螯篚痧戾礤铘犰徜怛遽朦徵彗狒妩疳玳钺糸镱疳珏螯痫瘐瘘镯蝈盹翦ㄤ彐疳蜥礤翦铒蝈盹鲥沆狍箢犴瀛蝈珏皙⑨钿狎糸沆妩怙澌泔祯眍泔铘孱酎磲轭箬徜秣ㄤ彐疳蜥礤翦怙矬舡沆狍箢犴瀛蝈珏皙⑨螋殂戾怙澌泔铘孱酎孱趄桢铘蝙璀孱趄磲轭疳珏疳玳钺糸镱痫篝翦酎忪镧篝矧ㄤ彐疳蜥礤翦篚痧蝈篌沆狍箢犴瀛蝈珏皙㈣殇溴铧掼殇栝浃栝掼殇忉铑弪泔礅泔眄孱酎泔憝泔铘徙酎骘雉骘雉弪骘雉铒翦玟痱磲篝桢徜礤溟狳礤翎秕翕蜥轭痱镯稂蝈灬翦潼筱蝻祆箬狎妩箬秕翕秫箝溴忉螯箅筱蜥疱螯箴镱箫螯箬镳痖铉翎珞麸镬鏖溏弭鏖翳梏盱筱矧瀛泔铘孱舡痱雉镢镬ㄨ繇飙沆狍箢犴瀛筱矧瀛痱雉镢镬痱雉镢镬屐屙孱汜汨濠ㄤ彐梏盱筱矧瀛泔铘孱ê屐屙孱ê骘蝽烘殄熹箦猴怅邈哄礅邃烘镲翦红轭忽殇屣洪骝犴洪铕豸呼屮翎蝈后屐邈衡豸麸瞟鲠祯弩ī铋飑┅ㄤ彐礤翳镤梏盱筱矧瀛泔铘孱舡溟箴狒汨è痱雉镢镬梏盱沆狍箢犴瀛筱矧瀛痱雉镢镬豉疱ㄥ耢哄戾礤铘┅翎屐屙孱汜汨濠戾舄è沆狍蟓狒趄殁豸ㄨ繇斓疳蝮弪哄戾礤铘狒趄殁豸屐屙孱沆狍螈┅ㄩ篝蜷铉滹黝汜箦ㄨ繇斓疳蝮弪哄戾礤铘狒趄殁豸屐屙孱㈤洧┅ㄣ灬篌弩ㄣ飙痧泸搴箴扉ㄦ矧磲铋③狺莴矧麒轸弩疳沐扉篝扉篝＼羽徙濠┅篝蜷铉滹黝汜箦沆狍蟓狒趄殁豸濠┅ㄢ镲篝ǐㄩㄦ轭洵殒灬礅溽ㄣ飙痧泸搴筱犷怙矬舡沆狍箢犴瀛蝈珏皙┅沆狍箦螬驳癌ㄩㄦ轭洵殒灬礅溽ㄣ飙痧泸搴筱犷篚痧蝈篌沆狍箢犴瀛蝈珏皙┅沆狍箦螬驳癌ㄩㄣ飙痧泸搴筱犷怙矬舡沆狍箢犴瀛蝈珏皙殇驳癌ㄩㄣ飙痧泸搴筱犷篚痧蝈篌沆狍箢犴瀛蝈珏皙殇驳癌┅ㄤ蝻ㄡ钿铒ㄣ飙痧泸搴筱犷铒蝈盹鲥沆狍箢犴瀛蝈珏皙殇┅铒ㄦ轭洵殒灬礅溽ㄣ飙痧泸搴筱犷铒蝈盹鲥沆狍箢犴瀛蝈珏皙┅沆狍箦螬矧ㄣ飙痧泸搴筱犷蝈盹鲠忪瀛沆狍箢犴瀛蝈珏皙殇ㄦ轭洵殒灬礅溽ㄣ飙痧泸搴筱犷蝈盹鲠忪瀛沆狍箢犴瀛蝈珏皙┅沆狍箦螬┅┅ㄣ镱ㄤ蝻鲠祯弩ī铋飑眭祠轲戾鲠祯瀛忾钿筱矧篚怏泔蝈疳篌翦泔铘孱舂ㄣ犰飙铄舡礤翳镤戾舄è怙矬翦ǐ筱矧怙矬舂ㄣ矧瀛筱矧矧ㄣ狎篚怏泔蝈螬癌疳篌怙矬翦ǐ泔蝈筱矧ㄩ怙矬癌ǒ怙矬博┅┅鲠祯弩怙矬翦ㄣ镱疳篌怙矬翦ㄣ潋篚怏泔蝈螬疳篌翦泔铘孱舂┅┅┅